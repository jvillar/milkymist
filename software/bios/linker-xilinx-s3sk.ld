OUTPUT_FORMAT("elf32-lm32")
ENTRY(_start)

__DYNAMIC = 0;

MEMORY {
	flash : ORIGIN = 0x00000000, LENGTH = 0x800000   /* 8M */
	sram  : ORIGIN = 0x20000000, LENGTH = 0x1000     /* 4k */
	sdram_bot : ORIGIN = 0x40000000, LENGTH = 0x1000000  /* 16M */
	sdram_top : ORIGIN = 0x41000000, LENGTH = 0x1000000  /* 16M */
}

SECTIONS
{
	.text :
	{
		_ftext = .;
		*(.text .stub .text.* .gnu.linkonce.t.*)
		_etext = .;
	} > flash

	.rodata :
	{
		. = ALIGN(4);
		_frodata = .;
		*(.rodata .rodata.* .gnu.linkonce.r.*)
		*(.rodata1)
		_erodata = .;
	} > flash

	.data :
	{
		. = ALIGN(4);
		_fdata = .;
		*(.data .data.* .gnu.linkonce.d.*)
		*(.data1)
		_gp = ALIGN(16);
		*(.sdata .sdata.* .gnu.linkonce.s.*)
		_edata = .;
	} > flash

	.bss :
	{
		. = ALIGN(4);
		_fbss = .;
		*(.dynsbss)
		*(.sbss .sbss.* .gnu.linkonce.sb.*)
		*(.scommon)
		*(.dynbss)
		*(.bss .bss.* .gnu.linkonce.b.*)
		*(COMMON)
		_ebss = .;
		_end = .;
	} > sram

	eth_data :
	{
		. = ALIGN(4);
		*(ETH_DATA)
	} > sdram_top
}

PROVIDE(_fstack = ORIGIN(sram) + LENGTH(sram) - 4);

